#set term qt size 1500, 525
set term epslatex color size 10in, 3.5in linewidth 2
set output "sensmulti_MW.tex"

#ytics margin size
MT = 0.04
#spacing between plots
MP = (1-MT)/3.0

set tmargin 0
set rmargin 0
set bmargin 2
#set lmargin 0
set xtics 0.01, 10, 10 nomirror
set xtics add (0.05, 0.5)
set ytics 1e-12, 100, 1 nomirror
set xlabel "Mass (GeV)"
set log xy
set xrange [0.01:2]
set yrange [5e-11:1]
set key samplen 1 spacing 1.2 top left reverse Left
#set multiplot layout 1, 3 spacing screen 0
set multiplot
#plot 1
set lmargin at screen MT
set size MT+MP, 1
set origin 0, 0
set format y "\\np\{e%T\}"
set ylabel "$ |U_{\\mu 4}|^2 $" 
plot "m/nEE_M_m_U.dat" u 1:2 w l t "\$\\nu e e \$ Majorana" lw 2 lc 1, "m/nEE_W_M_m_U.dat" u 1:2 w l not lw 2 lc 1 dt 3, "d/nEE_M_d_U.dat" u 1:2 w l t "\$\\nu e e \$ Dirac" lw 2 lc 2, "d/nEE_W_M_d_U.dat" u 1:2 w l not lw 2 lc 2 dt 3, \
"m/nMM_M_m_U.dat" u 1:2 w l t "\$\\nu \\mu \\mu \$ Majorana" lw 2 lc 3, "m/nMM_W_M_m_U.dat" u 1:2 w l not lw 2 lc 3 dt 3, "d/nMM_M_d_U.dat" u 1:2 w l t "\$\\nu \\mu \\mu \$ Dirac" lw 2 lc 4, "d/nMM_W_M_d_U.dat" u 1:2 w l not lw 2 lc 4 dt 3
#plot 2
set key bottom right
set lmargin 0
set size MP, 1
set origin MT+MP, 0
set format y ""
unset ylabel
set xrange [0.1:2]
plot "m/MPI_M_m_U.dat" u 1:2 w l t "\$e \\pi \$ Maj" lw 2 lc 1, "m/MPI_W_M_m_U.dat" u 1:2 w l not lw 2 lc 1 dt 3, \
     "d/MPI_M_d_U.dat" u 1:2 w l t "\$e \\pi \$ Dir" lw 2 lc 2, "d/MPI_W_M_d_U.dat" u 1:2 w l not lw 2 lc 2 dt 3, \
     "m/nEM_M_m_U.dat" u 1:2 w l t "\$\\nu e \\mu \$ Maj" lw 2 lc 3, "m/nEM_W_M_m_U.dat" u 1:2 w l not lw 2 lc 3 dt 3, \
     "d/nEM_M_d_U.dat" u 1:2 w l t "\$\\nu e \\mu \$ Dir" lw 2 lc 4, "d/nEM_W_M_d_U.dat" u 1:2 w l not lw 2 lc 4 dt 3
#plot 3
set key top left
set lmargin 0
set xrange [0.01:2]
set size MP, 1
set origin MT+2*MP, 0
set format y ""
unset ylabel
set xtics add (2)
plot "m/nPI0_M_m_U.dat" u 1:2 w l t "\$\\nu \\pi^0 \$ Majorana" lw 2 lc 3, "m/nPI0_W_M_m_U.dat" u 1:2 w l not lw 2 lc 3 dt 3, "d/nPI0_M_d_U.dat" u 1:2 w l t "\$\\nu \\pi^0 \$ Dirac" lw 2 lc 4, "d/nPI0_W_M_d_U.dat" u 1:2 w l not lw 2 lc 4 dt 3
unset multiplot
unset output
#plot "m/nGAMMA_M_m_U.dat" u 1:2 w l t "\$\\nu \\gamma \$ Majorana" lw 2 lc 1, "m/nGAMMA_W_M_m_U.dat" u 1:2 w l not lw 2 lc 1 dt 3, "d/nGAMMA_M_d_U.dat" u 1:2 w l t "\$\\nu \\gamma \$ Dirac" lw 2 lc 2, "d/nGAMMA_W_M_d_U.dat" u 1:2 w l not lw 2 lc 2 dt 3, \
